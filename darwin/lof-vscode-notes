#!/bin/bash

# Encoding: utf-8
#
# @raycast.title LOF Code Notes
# @raycast.author Connor Rhodes
# @raycast.description launch or focus vscode notes
# @raycast.mode silent
#
# @raycast.schemaVersion 1
WINDOW_NAME="notes"

/usr/bin/osascript -e '
on run argv
    set windowName to item 1 of argv
    try
        tell application "Visual Studio Code"
            -- Check if VS Code is already frontmost
            if frontmost then
                -- Check if the notes window exists and is the front window
                tell application "System Events"
                    tell application process "Code"
                        set targetWindow to (first window whose name is windowName)
                        if exists targetWindow then
                            set frontWindow to front window
                            if name of frontWindow is windowName then
                                -- The notes window is focused, minimize just this window
                                click button 3 of targetWindow
                                return
                            end if
                        end if
                    end tell
                end tell
            end if

            -- Either VS Code is not frontmost or notes window is not the front window
            activate
        end tell

        tell application "System Events"
            tell application process "Code"
                set targetWindow to (first window whose name is windowName)
                if exists targetWindow then
                    set frontmost to true
                    perform action "AXRaise" of targetWindow
                else
                    error "Window not found."
                end if
            end tell
        end tell
    on error
        do shell script "$HOME/.nix-profile/bin/code $HOME/code/notes"
    end try
end run
' "$WINDOW_NAME"
